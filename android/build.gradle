buildscript {
    ext.kotlin_version = '1.9.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext {
    // Provide defaults expected by some plugins (e.g. flutter_blue_plus_android)
    flutter = [
        compileSdkVersion: 34,
        compileSdk      : 34,
        targetSdkVersion: 34,
        targetSdk       : 34,
        minSdkVersion   : 21,
        minSdk          : 21
    ]
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    if (!project.ext.has('flutter')) {
        project.ext.flutter = rootProject.ext.flutter
    }

    project.plugins.whenPluginAdded { plugin ->
        if (plugin.class.name.startsWith("com.android.build.gradle.")) {
            project.android {
                if (!project.android.hasProperty("compileSdkVersion") || project.android.compileSdkVersion == null) {
                    compileSdkVersion rootProject.ext.flutter.compileSdkVersion
                }
                defaultConfig {
                    if (!defaultConfig.hasProperty("minSdkVersion") || defaultConfig.minSdkVersion == null) {
                        minSdk rootProject.ext.flutter.minSdkVersion
                    }
                    targetSdk rootProject.ext.flutter.targetSdkVersion
                }
            }
        }
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}

